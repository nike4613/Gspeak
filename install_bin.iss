; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "GSpeak"
#define MyAppVersion "2.6"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{EDC03466-5420-46C4-AED5-8151E57778EE}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
DefaultDirName={code:GetGmodInstall|C:\Program Files (x86)\Steam\steamapps\common\GarrysMod\}garrysmod\lua\bin
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
LicenseFile=LICENSE
; Remove the following line to run in administrative install mode (install for all users.)
PrivilegesRequired=lowest
OutputDir=
OutputBaseFilename=installer
Compression=lzma
SolidCompression=yes
WizardStyle=modern
CreateUninstallRegKey=no
Uninstallable=no

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "find_gmod_install.ps1"; DestDir: "this is meaningless"; Flags: dontcopy noencryption
Source: "unbom.ps1"; DestDir: "this is meaningless"; Flags: dontcopy noencryption
Source: "Client Plugin\Release\Win32\TSlib\gmcl_tslib_win32.dll"; DestDir: "{app}"; Flags:
Source: "Client Plugin\Release\x64\TSlib\gmcl_tslib_win64.dll"; DestDir: "{app}"; Flags:      
Source: "build\gspeak.ts3_plugin"; DestDir: "{tmp}"; Flags:

[Run]
Filename: "{tmp}\gspeak.ts3_plugin"; Flags: shellexec waituntilterminated  

[Code]
function GetGmodInstall(default: String): String;
var
  pwsh: String;
  S: AnsiString;
  R: Boolean;
  I: Integer;
begin
  ExtractTemporaryFile('find_gmod_install.ps1');
  ExtractTemporaryFile('unbom.ps1');
  pwsh := ExpandConstant('{sys}\WindowsPowerShell\v1.0\powershell.exe');
  R := Exec(pwsh, ExpandConstant('-nologo -noprofile -command ".\find_gmod_install.ps1 > .\output.txt"'),
    ExpandConstant('{tmp}'), SW_HIDE, ewWaitUntilTerminated, I);
  if (not R) then begin
    Result := default;
    exit;
  end;
  R := Exec(pwsh, ExpandConstant('-nologo -noprofile -command ".\unbom.ps1 .\output.txt"'),
    ExpandConstant('{tmp}'), SW_HIDE, ewWaitUntilTerminated, I);
  if (not R) then begin
    Result := default;
    exit;
  end;
  R := LoadStringFromFile(ExpandConstant('{tmp}\output.txt'), S);
  if (not R) then begin
    Result := default;
    exit;
  end;
  S := Trim(S);                            
  Log(S);
  Result := S;
end;